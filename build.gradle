plugins {
    id 'java'
    id "io.freefair.lombok" version "6.6.1"
}

apply plugin: 'maven-publish'

group = 'jf.chromater'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'
    testImplementation "io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT"
    testImplementation 'org.mockito:mockito-core:4.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.7.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.117.1'
    testImplementation 'org.apache.logging.log4j:log4j-api:2.18.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.18.0'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.18.0'
}

test {
    useJUnitPlatform()
}

tasks.register('copyJarToTestPaper', Copy) {
    from "build/libs/Chromater-" + version + ".jar"
    into "testsv/paper/plugins"
}

tasks.register('copyJarToTestWaterfall', Copy) {
    from "build/libs/Chromater-" + version + ".jar"
    into "testsv/waterfall/plugins"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
